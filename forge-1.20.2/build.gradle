// Add shadow plugin
plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
}

eclipse {
    project {
        name = "WhitelistSync(Forge-1.20.2)"
    }
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaLanguageVersion.of(17) // Need this here so eclipse task generates correctly.

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

ext.buildNumber = System.getenv().BUILD_NUMBER ?: "Dev"

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        server {
            workingDirectory project.file('run').canonicalPath
            args '--nogui'
        }
    }
}

project.archivesBaseName = "${project.archivesBaseName}-forge-${minecraft_version}"

configurations {
    shade
    minecraftLibrary.extendsFrom shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Core library dependency
    shade project(path: ':WhitelistSyncCore', configuration: 'shadow')

    // Used for development. These are added by the user later.
    // Need "minecraftLibrary" so the libraries are used in the dev environment. Without them, the JDBC drivers won't be loaded.
    minecraftLibrary "curse.maven:mysqlconnector-561280:4684944"
    minecraftLibrary "curse.maven:sqliteconnector-560832:3582173"
}

processResources {
    filesMatching(['pack.mcmeta', 'mods.toml', 'META-INF/mods.toml', 'mixins.json', '*.mixins.json']) {
        expand project.properties
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    archiveClassifier = 'slim'
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '1', // We are version 1 of ourselves
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
}

shadowJar {
    archiveBaseName = "WhitelistSync"
    archiveClassifier = "forge-${minecraft_version}"
    destinationDirectory = file '../target'
    configurations = [project.configurations.shade]
    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}